% Ott grammar for the symbolic output of isla-footprint, based on the Rust isla-lib/srf/smt.rs types

% Warning: section delimited by "%%%%% NAME %%%%%" are used by non-ott tools to split the file
% in dependent projects like read-dwarf. If you change them, check dependent project still build
% In particular, all sections must be able to be independent ott files.

%%%%% terminals %%%%%

% SMT symbolic variables
metavar vvar ::=    {{ ocaml int }}
                    {{ coq PLACEHOLDER }}
                    {{ phantom }}
                    {{ lex numeric }}
                    {{ ocamllex 'v'['0'-'9']+ }}
                    {{ ocamllex-of-string (function s -> int_of_string (String.sub s 1 (String.length s -1))) }}
                    {{ pp-raw x = string (string_of_int x) }}
                    {{ pp x = string ("v"^string_of_int x) }}


% register and field names, lexed with surrounding | | but represented without
% (do we want to represent these as strings, or as indices into a symbol table?  the former is simpler for now)
metavar name ::=    {{ ocaml string }}
                    {{ coq PLACEHOLDER }}
                    {{ phantom }}
                    {{ lex alphanum }}
                    {{ ocamllex '|'[^'|''\\']+'|' }}
                    {{ ocamllex-of-string (function s -> String.sub s 1 (String.length s -2)) }}
                    {{ pp x = !^(Printf.sprintf "|%s|" x) }}

% enum names of the form en_m
metavar enum ::=    {{ ocaml int * int }}
                    {{ coq PLACEHOLDER }}
                    {{ lex alphanum }}
                    {{ ocamllex 'e'['0'-'9']+'_'['0'-'9']+ }}
                    {{ ocamllex-of-string (function s -> Scanf.sscanf s "e%[0-9]_%[0-9]" (fun a b -> (int_of_string a, int_of_string b))) }}
                    {{ pp-raw x = let (a,b) = x in !^(Printf.sprintf "%d/%d" b a) }}
                    {{ pp x = let (a,b) = x in !^(Printf.sprintf "e%d_%d" a b) }}

% enum type names of the form Enum
metavar enum_ty ::= {{ ocaml int  }}
                    {{ coq PLACEHOLDER }}
                    {{ phantom }}
                    {{ lex alphanum }}
                    {{ ocamllex 'E''n''u''m'['0'-'9']+ }}
                    {{ ocamllex-of-string (function s -> Scanf.sscanf s "Enum%d" Fun.id) }}
                    {{ pp-raw x = !^(Printf.sprintf "Enum(%d)" x) }}
                    {{ pp x = !^(Printf.sprintf "Enum%d" x) }}





% Just a plain integer
metavar int ::=     {{ ocaml int }}
                    {{ coq PLACEHOLDER }}
                    {{ phantom }}
                    {{ lex numeric }}
                    {{ ocamllex ['0'-'9']+ }}
                    {{ ocamllex-of-string int_of_string }}
                    {{ pp x = string (string_of_int x) }}
                    {{ pp-raw x = string (string_of_int x) }}


% integer bitvectors. "bv-24" is the bitvector that represents 24
metavar bvi ::=     {{ ocaml int }}
                    {{ coq PLACEHOLDER }}
                    {{ phantom }}
                    {{ lex numeric }}
                    {{ ocamllex ('b''v''-'?['0'-'9']+) }}
                    {{ ocamllex-of-string (function s -> int_of_string (String.sub s 2 (String.length s -2))) }}
                    {{ pp-raw x = string (string_of_int x) }}
                    {{ pp x = string ("bv"^string_of_int x) }}

% bitvector litteral
metavar bv ::=      {{ ocaml string }}
                    {{ coq PLACEHOLDER }}
                    {{ phantom }}
                    {{ lex numeric }}
                    {{ ocamllex ('#''b'['0'-'1']+)|('#''x'['0'-'9''a'-'f''A'-'F']+) }}
                    {{ pp x = string (x) }}
                    {{ pp-raw x = string (x) }}

% String litteral
metavar str ::=     {{ ocaml string }}
                    {{ coq PLACEHOLDER }}
                    {{ phantom }}
                    {{ lex alphanum }}
                    {{ ocamllex '\"'[^'\"']*'\"' }}
                    {{ pp x = string (x) }}
                    {{ pp-raw x = string (x) }}

indexvar j,k ::=    {{ ocaml int }}
                    {{ coq PLACEHOLDER }}
                    {{ phantom }}

%%%%% parser locations %%%%%
embed
{{ ocaml

type lrng =
  | UnknownRng
 (* | Int of string * lrng option *)
  | Generated of lrng
  | Range of Lexing.position * Lexing.position

(* TODO figure out how to use that *)
exception Parse_error_locn of lrng * string

let pp_lpos p =
  PPrint.string
    Lexing.(
      Printf.sprintf "File \"%s\", line %d, character %d" p.pos_fname p.pos_lnum
        (p.pos_cnum - p.pos_bol))

let rec pp_lrng l =
  match l with
  | UnknownRng -> PPrint.string "Unknown position"
  | Generated l -> PPrint.prefix 2 1 (PPrint.string "Generated:") (pp_lrng l)
  | Range (p1, p2) ->
      Lexing.(
        assert (p1.pos_fname = p2.pos_fname);
        if p1.pos_lnum = p2.pos_lnum then begin
          assert (p1.pos_bol == p2.pos_bol);
          PPrint.string
          @@ Printf.sprintf "File \"%s\", line %d, character %d-%d" p1.pos_fname p1.pos_lnum
               (p1.pos_cnum - p1.pos_bol) (p2.pos_cnum - p2.pos_bol)
        end
        else
          PPrint.string
          @@ Printf.sprintf "File \"%s\", line %d-%d" p1.pos_fname p1.pos_lnum p2.pos_lnum)
   (* | Int(s,lo) -> Printf.sprintf "Int: %s %s" s (match lo with Some l -> pp_lrng l | None -> "none") *)

}}

%%%%% annotations %%%%%

% annotation are lrng by default can be something else. They are not pretty-printed

grammar
% TODO handle pretty printing of annotations
annot :: '' ::=                         {{ phantom }}
                                        {{ ocaml 'a }}
                                        {{ com Annotation }}
                                        {{ pp l = PPrint.empty }}
                                        {{ pp-raw l = PPrint.empty }}






%%%%% types %%%%%
grammar
ty :: 'Ty_' ::=
  | Bool                                :: :: Bool
  | (_ BitVec int )                     :: :: BitVec
  | enum_ty                             :: :: Enum


%%%%% bool %%%%%
grammar
bool :: '' ::=                          {{ ocaml bool }}
                                        {{ phantom }}
                                        {{ pp b = if b then string "true" else string "false" }}
                                        {{ pp-raw b = if b then string "true" else string "false" }}
  | true                                :: :: True {{ ocaml true }}
  | false                               :: :: False {{ ocaml false }}

%%%%% operations %%%%%
grammar

% Operations that must be applied to exactly one element
unop :: '' ::=
  | not                                 :: :: Not
  | bvnot                               :: :: Bvnot
  | bvredand                            :: :: Bvredand
  | bvredor                             :: :: Bvredor
  | bvneg                               :: :: Bvneg
  | (_ extract int int' )               :: :: Extract
  | (_ zero_extend int )                :: :: ZeroExtend
  | (_ sign_extend int )                :: :: SignExtend

% Arithmetic operations that must be applied to exactly two elements of the same size
bvarith :: '' ::=
  | bvnand                              :: :: Bvnand
  | bvnor                               :: :: Bvnor
  | bvxnor                              :: :: Bvxnor
  | bvsub                               :: :: Bvsub
  | bvudiv                              :: :: Bvudiv
  | bvudiv_i                            :: :: Bvudivi
  | bvsdiv                              :: :: Bvsdiv
  | bvsdiv_i                            :: :: Bvsdivi
  | bvurem                              :: :: Bvurem
  | bvsrem                              :: :: Bvsrem
  | bvsmod                              :: :: Bvsmod
  | bvshl                               :: :: Bvshl
  | bvlshr                              :: :: Bvlshr
  | bvashr                              :: :: Bvashr


% Comparison operations, they must be applied to exactly two elements of the same size
bvcomp :: '' ::=
  | bvult                               :: :: Bvult
  | bvslt                               :: :: Bvslt
  | bvule                               :: :: Bvule
  | bvsle                               :: :: Bvsle
  | bvuge                               :: :: Bvuge
  | bvsge                               :: :: Bvsge
  | bvugt                               :: :: Bvugt
  | bvsgt                               :: :: Bvsgt


% Operations that must be applied to exactly two elements
binop :: '' ::=
  | =                                   :: :: Eq
  | neq                                 :: :: Neq
  | bvarith                             :: :: Bvarith
  | bvcomp                              :: :: Bvcomp

% Arithmetic operations that can be applied to 2 or more elements of the same size
bvmanyarith :: '' ::=
  | bvand                               :: :: Bvand
  | bvor                                :: :: Bvor
  | bvxor                               :: :: Bvxor
  | bvadd                               :: :: Bvadd
  | bvmul                               :: :: Bvmul

% Operations that can be applied to 2 or more elements
manyop :: '' ::=
  | and                                 :: :: And
  | or                                  :: :: Or
  | bvmanyarith                         :: :: Bvmanyarith
  | concat                              :: :: Concat


%%%%% exp %%%%%
grammar
exp :: '' ::=                           {{ aux _ annot }}
                                        {{ auxparam 'a }}
                                        {{ menhir-start }}
                                        {{ menhir-start-type (Isla_lang_ast.lrng) Isla_lang_ast.exp }}
  | vvar                                :: :: Var
  | bv                                  :: :: Bits
  | bool                                :: :: Bool
  | enum                                :: :: Enum
  | ( unop exp )                        :: :: Unop
  | ( binop exp1 exp2 )                 :: :: Binop
  | ( manyop exp1 .. expj )             :: :: Manyop
  | ( ite exp1 exp2 exp3 )              :: :: Ite


%%%%% smt %%%%%
grammar
smt :: '' ::= {{auxparam 'a }}
  | ( declare-const vvar ty )           :: :: DeclareConst
  | ( define-const vvar exp )           :: :: DefineConst
  | ( assert exp )                      :: :: Assert
     % (define-enum n) is a sugar for
     % (declare-datatypes () ((Enum_n en_0 en_1 ... en_(n-1) ))) but
     % it can appear multiple times and the type must be declared only once
  |  ( define-enum int )                :: :: DefineEnum



%%%%% register values %%%%%

% Those are the value stored inside isla symbolic value.
% Those can contain value of type that can't appears in expressions like unit
% or string.

grammar
valu :: 'Val_' ::=
 | vvar                                 :: :: Symbolic
 | bool                                 :: :: Bool
 | (_ bvi int )                         :: :: I
 | bv                                   :: :: Bits
 | enum                                 :: :: Enum
 | str                                  :: :: String
 | (_ unit )                            :: :: Unit
 | ( name (_ unit))                     :: :: NamedUnit
 | (_ vec valu1  ..  valuk  )           :: :: Vector  % TODO: fix syntax
 | (_ list valu1 .. valuk )             :: :: List  % TODO: fix syntax
 | (_ struct  selem1 .. selemk )        :: :: Struct
 | (_ poison )                          :: :: Poison

% structure element
selem :: '' ::=                         {{ phantom }}
                                        {{ ocaml string * valu }}
                                        {{ pp se = let (n,v) = se in
                                                   parens (pp_name n ^^ blank 1 ^^ pp_valu v) }}
                                        {{ pp-raw se = let (n,v) = se in
                                                       pp_raw_name n ^^ !^"," ^^ pp_raw_valu v }}
 | ( name valu )                        :: :: Struct_elem {{ ocaml (name,valu) }}

%%%%% events %%%%%
grammar

% TODO support vector register access.
accessor :: '' ::=
  | (_ field name )                                                             :: :: Field

accessor_list :: '' ::=
  | nil                                                                         :: :: Nil
  | ( accessor1 .. accessork )                                                  :: :: Cons

% The matching data type is `Event` in `isla-lib/src/smt.rs`
% The order of constructors should match the one of the `Event` definition.
% The pretty printing function that need to be parsed by this is
% `write_events_with_opts` in `isla-lib/src/simplify.rs`
event :: '' ::=  {{ aux _ annot }} {{auxparam 'a }}
  |  smt                                                                        :: :: Smt
  |  ( branch int str )                                                         :: :: Branch
  |  ( read-reg name accessor_list valu )                                       :: :: ReadReg
  |  ( write-reg name accessor_list valu )                                      :: :: WriteReg
   % ( read_mem result kind  addr   size)
  |  ( read-mem valu   valu' valu'' int )                                       :: :: ReadMem
   % ( write_mem success kind  addr  data   size )
  |  ( write-mem vvar    valu  valu' valu'' int' )                              :: :: WriteMem
  |  ( branch-address valu )                                                    :: :: BranchAddress
  |  ( barrier valu )                                                           :: :: Barrier
   % ( cache-op kind addr )
  |  ( cache-op valu valu' )                                                    :: :: CacheOp
  |  ( mark-reg name str )                                                      :: :: MarkReg
  |  ( cycle )                                                                  :: :: Cycle
  |  ( instr valu )                                                             :: :: Instr
  |  ( sleeping vvar )                                                          :: :: Sleeping
  |  ( wake-request )                                                           :: :: WakeRequest
  |  ( sleep-request )                                                          :: :: SleepRequest

%%%%% traces %%%%%

grammar

trc :: '' ::=                 {{ menhir-start }}
                              {{ auxparam 'a }}
                              {{ menhir-start-type Isla_lang_ast.lrng Isla_lang_ast.trc }}
 | ( trace event1 .. eventj )                                                   :: :: Trace
